apply plugin: 'com.android.library'

def safeExtGet(prop, fallback) {
  rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

android {
  def agpVersion = com.android.Version.ANDROID_GRADLE_PLUGIN_VERSION
  if (agpVersion.tokenize('.')[0].toInteger() >= 7) {
    namespace "org.reactnative.camera"
  }

  compileSdkVersion safeExtGet('compileSdkVersion', 34)
  buildToolsVersion safeExtGet("buildToolsVersion", "34.0.0")

  defaultConfig {
    minSdkVersion safeExtGet('minSdkVersion', 21)
    targetSdkVersion safeExtGet('targetSdkVersion', 34)
    multiDexEnabled true
  }

  lintOptions{
    abortOnError false
  }

  flavorDimensions "react-native-camera"

  productFlavors {
    general {
      dimension "react-native-camera"
      namespace "org.reactnative.camera"
    }
    mlkit {
      dimension "react-native-camera"
      namespace "org.reactnative.camera"
    }
  }

  packagingOptions {
    resources {
      excludes += [
              'META-INF/androidx.exifinterface_exifinterface.version',
              'META-INF/proguard/androidx-annotations.pro'
      ]
    }
  }
}

repositories {
  google()
  mavenCentral()
  maven { url "https://jitpack.io" }
  maven { url "$rootDir/../node_modules/react-native/android" }
}

dependencies {
  implementation 'com.facebook.react:react-native:+'
  implementation "com.google.zxing:core:3.3.3"
  implementation "com.drewnoakes:metadata-extractor:2.11.0"
  implementation "androidx.exifinterface:exifinterface:1.3.2"
  implementation "androidx.annotation:annotation:1.0.0"
  implementation "androidx.legacy:legacy-support-v4:1.0.0"
  implementation "com.google.android.gms:play-services-mlkit-text-recognition:19.0.1"
  generalImplementation "com.google.android.gms:play-services-mlkit-barcode-scanning:18.3.1"
  generalImplementation "com.google.android.gms:play-services-mlkit-face-detection:17.1.0"
  mlkitImplementation "com.google.mlkit:barcode-scanning:17.3.0"
  mlkitImplementation "com.google.mlkit:face-detection:16.1.7"
}
